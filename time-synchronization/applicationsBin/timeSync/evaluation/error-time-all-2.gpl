set terminal pdf font ' , 8 ' size 13cm,6cm
#default size:
#size 12.7cm,7.6cm

set output "graphs/error-time-all.pdf"

set termoption dash
#set for [i=1:7] linetype i dt i
#set linetype 3 dt 9

set xlabel "Time (min)"
set ylabel "Maximum pairwise synchronization error (ms)"

diams = "10 30 54"
names = "5 15 27"
modules = "231 4,991 27,775 "
dir = "results/Ball/"
file = "error."

rescaleY(t) = t/1000
rescaleX(t) = (t/1000/1000/60)

#set style fill transparent solid 0.5

set style line 1 lt 1 lc rgb "red" lw 1 pt 1
set style line 2 lt 2 lc rgb "blue" lw 1 pt 2
set style line 3 lt 3 lc rgb "#8B4513" lw 1 pt 3  # brown
set style line 4 lt 4 lc rgb "#9ACD32" lw 1 pt 4
set style line 5 lt 5 lc rgb "orange" lw 1 pt 5
set style line 6 lt 6 lc rgb "purple" lw 1 pt 6
#set style line 7 lt 7 lc rgb "cyan" lw 1 pt 7
set style line 7 lt 7 lc rgb "#7700FFFF" lw 1 pt 7
set style line 8 lt 8 lc rgb "magenta" lw 1 pt 8

set grid xtics ytics
#mxtics ytics
set mytics 10
set mxtics 5

set format y "10^{%.0T}"
set ytics add ("1" 1)
set logscale y

set macros
PLOT_DATA = "plot [58:120][1:1000000] folder.'mrtp-e2ace' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 1 title 'MRTP-PC2LE-PRED',\
     folder.'mrtp-diameter' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 2 title 'MRTP-MINID-PRED',\
     folder.'tpsn-e2ace-mle' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 3 title 'MLE_TPSN-PC2LE-RTT',\
     folder.'wmts' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 4 title 'WMTS-PRED',\
     folder.'ats' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 5 title 'ATS-PRED',\
     folder.'ftsp-diameter' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 6 title 'FTSP-PRED',\
     folder.'PulseSync' using (rescaleX($1)):(rescaleY($2)) smooth unique linestyle 7 title 'PulseSync-PRED'

set multiplot layout 1,3

#plot for [i=1:words(files)] word(files, i).'.dat'

set bmargin 8

do for [i=1:words(diams)] {
name = word(diams, i)
folder = dir.name.'/'.file
radius = word(names,i)
diameter = word(diams,i)
module = word(modules,i)
unset arrow
unset label

#set label 11 center at graph 0.5,char 1.75 'Ball('.radius.')' font ',10'
set label 11 center at graph 0.5,char 3.5 'Ball('.radius.")\n".diameter." hop-diameter\n". module.' modules' font ',10' enhanced

if (i == 1) {
  #set key samplen 2
  set key inside at 117,60000 noenhanced
  set arrow from 80,500000 to 63,50000 head
  #set arrow from 80,500000 to 61,40000 head	
  set label 12 "Synchronization start" at 80,500000
} else {
  set nokey
  if (i == 2) {
  unset label 12
  unset arrow
  #set arrow from 67,6000 to 60.5,59000 head
  #set label 12 "Synchronization start" at 63.5,5000 

  set arrow from 80,500000 to 62,80000 head
  set label 12 "Synchronization start" at 80,500000
  } else {
  unset label 12
  unset arrow   
  
  set arrow from 75,20000 to 62,50000 head
  set label 12 "Synchronization start" at 76,20000
  
  #set arrow from 75,30000 to 62,55000 head
  #set label 12 "Synchronization start" at 76,20000
  }
}

@PLOT_DATA

}
unset multiplot 
unset label 11

set term x11
set terminal pdf size 7cm,9cm
set output "graphs/error-time-all-2-2.pdf"
#size 10cm,10cm font ",12"
#font ' , 8 ' 
set multiplot layout 2,2

unset key

do for [i=1:words(diams)] {
set bmargin 4
name = word(diams, i)
radius = word(names,i)
diameter = word(diams,i)
module = word(modules,i)
folder = dir.name.'/'.file

set title 'Ball('.radius.')\n'.diameter.' hop-diameter\n'. module.' modules'
#set label 11 center at graph 0.5,-0.1 'Ball('.name.')'

@PLOT_DATA

}

# Legend (empty plot)
unset ylabel
unset xlabel
unset tics
unset border

#set bmargin at screen 0
#set tmargin at screen 0.2
#set lmargin at screen 0
#set rmargin at screen 1

unset label 11
unset title
#set key font ",12"

set key center vertical box opaque noenhanced title "Protcols:"
#set key samplen 5

plot [][1:80] NaN linestyle 1 title 'MRTP-PC2LE-PRED',\
     NaN linestyle 2 title 'MRTP-MIN_ID-PRED',\
     NaN linestyle 3 title 'MLE_TPSN-PC2LE-RTT',\
     NaN linestyle 4 title 'AD-PRED',\
     NaN linestyle 5 title 'ATS-PRED',\
     NaN linestyle 6 title 'FTSP-PRED'
     #,\
     #NaN linestyle 8 title 'AD'

unset multiplot
