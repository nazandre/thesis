set terminal pdf mono enhanced font ",16"

set termoption enhanced

set key inside top left vertical Right title "Goal shape"

set xlabel "Configuration size (2D Catom)"
set ylabel "Simulated time (min)"

set key title "Goal shape"


#set format x "%.2t*10^%+03T"

#set logscale x

set mxtics 3
set mytics 5

set xtics 3000
#set format x "%.1tx10^{%.0T}"
#set xtics add ("0" 0)

set grid xtics ytics
set output "time.pdf"

# convert us in min
convertTime(x) = x/1000/1000/60

#linear fit

f(x) = a*x + b
fit f(x) '../results/global-summary.out' using 1:(convertTime($2)) via a, b
ti = sprintf("y(x) = %.3fx+%.3f", a, b)
set label ti at 6000,70 rotate by 31

#set label "Slope {/Symbol \273} 0.017 Catoms/min {/Symbol \273} 1.034 Catoms/sec " at 2500,15 rotate by 31

# [0:200]
plot '../results/car/summary.out' using 1:(convertTime($2)):(convertTime($3)) with yerrorbars title "car" pt 1 lt 1,\
'../results/flag/summary.out' using 1:(convertTime($2)):(convertTime($3)) with yerrorbars title "flag" pt 2 lt 2,\
'../results/magnet/summary.out' using 1:(convertTime($2)):(convertTime($3)) with yerrorbars title "magnet" pt 3 lt 3,\
'../results/pyramid/summary.out' using 1:(convertTime($2)):(convertTime($3)) with yerrorbars title "pyramid" pt 4 lt 4,\
f(x) title "" with lines
