set terminal pdf enhanced size 20cm,20cm font ",19"

load 'variants.gpl'

set key inside center bottom vertical Right noreverse enhanced autotitles nobox width 3 opaque

set xlabel "Size (module)"

set mxtics 5
set mytics 2
#set xtics 200
set grid xtics ytics

set logscale x
set xrange [10:]
set format x "10^{%.0T}"

decr(i) = i - 1
dir = "data/bb/r/"

set output "memory-queue.pdf"
set ylabel "Maximum memory\nusage per node (byte)"

w = 0.5
h = 0.5

set size h,w

set multiplot layout 3,2

set yrange [250:500]

unset key

yTitleOffset=-0.33

set title "Best algorithms (usage < 500 bytes)" offset 0,yTitleOffset

set tmargin at screen 0.9
set bmargin at screen 0.65

plot for [i=1:words(variants)] dir.word(variants,i).'.memory' using 1:2 with linespoint title word(names,i) linestyle i

set logscale y
set format y "10^{%.0T}"
unset yrange
set mytics 5
set yrange [5*10**2:]

set title "Worst algorithms (usage > 500 bytes)"  offset -1,yTitleOffset

plot for [i=1:words(variants)] dir.word(variants,i).'.memory' using 1:2 with linespoint title word(names,i) linestyle i

#### Queue

set tmargin at screen 0.45
set bmargin at screen 0.2

set yrange [12:21]
set mytics 1
unset logscale y
unset format y

set ylabel "Maximum message\nqueue size (message)" offset 0,yTitleOffset
set title "Best algorithms (queue length < 21)"

plot for [i=1:words(variants)] dir.word(variants,i).'.queue' using 1:2 with linespoint title word(names,i) linestyle i

set logscale y
set format y "10^{%.0T}"
set yrange [30:10**4]
set mytics 5
set title "Worst algorithms (queue length > 30)" offset -1,yTitleOffset
set size h,w

plot for [i=1:words(variants)] dir.word(variants,i).'.queue' using 1:2 with linespoint title word(names,i) linestyle i

#set key horizontal width 2 height 1 at screen 0.5,screen 0.1 center maxrows 2

#example key settings
unset ylabel
unset xlabel
unset tics
unset border
unset title

set lmargin 0
set rmargin 1

set key font ",16" horizontal nobox title "Algorithms" 
set key Left reverse samplen 3 width -2 maxcolumns 20
set key at screen 0.5,screen 0.12 center top

#We need to set an explicit xrange.  Anything will work really.
set xrange [1:2]
set yrange [1:2]

set arrow 1 from screen 0.1,screen 0.56 to screen 0.9, screen 0.56 nohead lc rgb 'black' lw 2
set label 1 "Maximum memory usage per node (algorithm variable + message queue)" at screen 0.5, screen 0.97 center
set label 2 "Maximum message queue size" at screen 0.5, screen 0.53 center

plot for [i=1:words(variants)] NaN title word(names,i) linestyle i with linespoints 

#plot for [i=1:words(errorEccVariants)]  NaN title word(errorNames,i) linestyle word(errorColorLineStyle,i) with linespoints

#plot for [i=1:words(errorEccVariants)] dir.word(errorEccVariants,i).'.errorEcc' using 1:(100 - $2*100) with yerrorlines title word(errorEccNames,i) linestyle i

unset multiplot
