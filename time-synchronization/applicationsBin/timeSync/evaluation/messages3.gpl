set terminal pdf
set output "graphs/messages3.pdf"

#see http://stackoverflow.com/questions/33022598/gnuplot-columnstacked-histogram-with-errorbars

#set title "Average number of messages sent per module"
set datafile missing "-"

#set logscale y

set style data histogram

set style histogram rowstacked

set lt 1 lc rgb "blue"
set lt 2 lc rgb "green"
set lt 3 lc rgb "red"

set style fill solid border
set boxwidth 0.75
unset xtics
set xtics nomirror rotate by -45 scale 0,0 font ",8" #autojustify
#offset 0, 10, 0.1
set xlabel " " offset 0,2
set ytics font ",8"
set grid y
set auto y
set auto x

dir = "messages/Ball/"

#<left>,<right>,<bottom>,<top>
#title "Average number of messages sent per module"

# to modify horizontal spacing between graphs: spacing X, ?Y?
set multiplot layout 2,3 margins 0.1,0.95,0,0.90 spacing 0.10,0.08
unset key

maxY = 4500

#set tmargin at screen 0
set bmargin at screen 0.45

set ylabel "Average number of messages per module" offset 3.25,0 font ",8"

plot [][0:maxY] newhistogram "Ball(5)\n231 modules" lt 1, dir.'10.dat' using "Leader":xtic(1) t "Leader election", '' u "Infrastructure" t "Infrastructure", '' u "Synchronization" t "Synchronization"

set bmargin at screen 0.45
#\n30-hop diameter
plot [][0:maxY] newhistogram "Ball(15)\n 4,991 modules" lt 1, dir.'30.dat' using "Leader":xtic(1) notitle, '' u "Infrastructure" notitle, '' u "Synchronization" notitle

set bmargin at screen 0.45
#\n54-hop diameter
plot [][0:maxY] newhistogram "Ball(27)\n27,775 modules" lt 1, dir.'54.dat' using "Leader":xtic(1) notitle, '' u "Infrastructure" notitle, '' u "Synchronization" notitle


unset ylabel
set xlabel " " offset 0,0
unset tics
unset border

set bmargin at screen 0
set tmargin at screen 0.2
set lmargin at screen 0
set rmargin at screen 1

set key center horizontal nobox opaque title "Types of message: " noenhanced
set key samplen 5

plot [][0:4000] newhistogram "" lt 1, NaN title "Leader Election" lw 10, NaN title "Infrastructure" lw 10, NaN title "Synchronization" lw 10

unset multiplot
